plugins {
    id 'com.github.zxhr.xtext-generator-eclipse-plugin'
    id 'com.github.zxhr.xtext-generator-eclipse-plugin-test'
}

import org.gradle.internal.os.OperatingSystem

repositories {
    ivy {
        url 'https://download.eclipse.org/releases/2020-09/202009161000/plugins/'
        patternLayout {
            artifact '[artifact]_[revision].[ext]'
        }
        metadataSources {
            artifact()
        }
    }
}

configurations.implementation {
    exclude module: 'org.eclipse.swt.${osgi.platform}'
    exclude group: 'com.sun.jna'
}

dependencies {
    implementation project(':example.mydsl')
    implementation project(':example.mydsl.ide')
    implementation platform("org.eclipse.xtext:xtext-dev-bom:$xtextVersion")
    implementation "org.eclipse.xtext:org.eclipse.xtext.ide:$xtextVersion"
    implementation 'org.eclipse.platform:org.eclipse.compare:3.7.1100'
    if (OperatingSystem.current().isWindows()) {
        compileOnly 'org.eclipse.platform:org.eclipse.swt.win32.win32.x86_64:3.115.0'
    } else if (OperatingSystem.current().isLinux()) {
        compileOnly 'org.eclipse.platform:org.eclipse.swt.gtk.linux.x86_64:3.115.0'
    } else if (OperatingSystem.current().isMacOsX()) {
        compileOnly 'org.eclipse.platform:org.eclipse.swt.cocoa.macosx.x86_64:3.115.0'
    }
    implementation name: 'org.eclipse.xtext.ui', version: '2.23.0.v20200831-0808', ext: 'jar'
    implementation name: 'org.eclipse.xtext.ui.codetemplates.ui', version: '2.23.0.v20200831-0808', ext: 'jar'
    implementation name: 'org.eclipse.xtext.ui.shared', version: '2.23.0.v20200831-0808', ext: 'jar'
    implementation name: 'org.eclipse.xtext.builder', version: '2.23.0.v20200831-0808', ext: 'jar'
    implementation name: 'org.eclipse.emf.edit', version: '2.16.0.v20190920-0401', ext: 'jar'
    implementation name: 'org.eclipse.emf.edit.ui', version: '2.18.0.v20200205-0529', ext: 'jar'

    testImplementation "org.eclipse.xtext:org.eclipse.xtext.testing:$xtextVersion"
}
